#!/usr/bin/awk -f
# Copyright Â© 2017-2018, Lukas Werling
# 
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
# 
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

BEGIN {
	OFS = "\t";
	print "type", "time_start", "time_end", \
			 "ultoverhead", \
			 "memory_bench", "cpu_bench", "memory_ratio", "cpu_ratio";
}

/start / {
	time_start = $1;
	type = gensub(/^.*start /, "", 1);
}

/start:/ { time_start = $1 }
/end:/ {
	time_end = $1;
	print type, time_start, time_end, \
	      "", \
	      memory_bench, cpu_bench, memory_ratio, cpu_ratio;
      memory_bench = cpu_bench = memory_ratio = cpu_ratio = "";
}

/end / && ultoverhead_count > 0 {
	time_end = $1;
	print type, time_start, time_end, \
	      ultoverhead / ultoverhead_count, \
	      memory_bench, cpu_bench, memory_ratio, cpu_ratio;
	ultoverhead_count = 0;
}

/for [0-9]+ migrations/ {
	ultoverhead += $6;
	ultoverhead_count += 1;
}

/^MEMORY_BENCH = / {
	memory_bench = $3;
	if ($4 == "@")
		memory_ratio = $5;
	else
		memory_ratio = 1;
}
/^CPU_BENCH = / {
	cpu_bench = $3
	if ($4 == "@")
		cpu_ratio = $5;
	else
		cpu_ratio = 1;
}

