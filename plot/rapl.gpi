# Copyright Â© 2017, Lukas Werling
# 
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
# 
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

set term qt noraise
# Prevent colors from repeating after 7 lines.
set palette rgb 33,13,10
unset colorbox

cmd(core) = sprintf('< awk -v core=%d ''/^Package / && core == -1 {print $4} /^Core / && +$2 == core {print $6}'' %s', core, @data)
core_title(core) = core == -1 ? "Package" : sprintf('Core %d', core)

plot for [core=-1:@maxcore] cmd(core) with lines \
	title core_title(core) \
	linetype palette frac (core + 1.) / (@maxcore + 1)

pause 1
reread
