#!/bin/zsh

set -eu

# Configuration

# CPUs used for ultmigration
export FAST_CPU=0 SLOW_CPU=2
# CPUs used for ult_idle during baseline tests.
IDLE_FAST_CPU=4
IDLE_SLOW_CPU=5
# CPU used for power monitoring.
OTHER_CPU=11

# Command for reading the power meter.
powermeter() {
	taskset -c $OTHER_CPU ssh i30pc34 sudo '~/src/ut71dmm/ut71dmm'
}

# test/micro parameters
MEMORY_BENCH=(indirect_access pointer_chasing)
CPU_BENCH=(some)
MEMORY_RATIO=(1 0.8 0.6)
CPU_RATIO=(1 0.8 0.6)

# Whether to run SWP.
RUN_SWP=0

# Number of runs for each benchmark type.
ITERATIONS=5

# Extra single frequency ids as comparison.
# Note: default "slow" is 50, "fast" is 144
CPU_FIDS=(125 106 88 69)

# Path to build directory
BUILD=../build

# End configuration

cd ${0:A:h}

benchname=${1?benchmark name not set}
bd=results/$benchname
if [[ -e "$bd" ]]; then
	echo "Benchmark $benchname already exists"
	exit 2
fi
mkdir $bd

# On exit, kill all background jobs.
zshexit TRAPINT() {
	kill -SIGHUP 0
}

log() {
	echo $@ >> $bd/log
}

# Print log file output to allow following progress.
taskset -c $OTHER_CPU tail -f $bd/log &

log Benchmark configuration
log FAST_CPU=$FAST_CPU SLOW_CPU=$SLOW_CPU
log OTHER_CPU=$OTHER_CPU
log MEMORY_BENCH $MEMORY_BENCH
log CPU_BENCH $CPU_BENCH
log MEMORY_RATIO $MEMORY_RATIO
log CPU_RATIO $CPU_RATIO
log ITERATIONS $ITERATIONS
log

log Setting P-states...
# The voltage levels are fixed for the three P-states on our platform.
sudo $BUILD/tools/amdpstate def 0 CpuFid=144 # 3600 MHz
sudo $BUILD/tools/amdpstate def 2 CpuFid=50  #  800 MHz

echo userspace | sudo tee /sys/devices/system/cpu/cpu[0-9]*/cpufreq/scaling_governor > /dev/null
# Set the first core to the middle p-state, rest to the last. (zsh arrays are indexed from 1)
frequencies=("${=$(< /sys/devices/system/cpu/cpu0/cpufreq/scaling_available_frequencies)}")
echo "${frequencies[3]}" | sudo tee /sys/devices/system/cpu/cpu[0-9]*/cpufreq/scaling_setspeed > /dev/null
echo "${frequencies[1]}" | sudo tee /sys/devices/system/cpu/cpu{0,1}/cpufreq/scaling_setspeed > /dev/null

sudo $BUILD/tools/amdpstate -call frequency 1000 >> $bd/log
log

log $(date -Ins) Benchmark start

# Add timestamp to input. Simple reimplementation of ts from moreutils.
ts() {
	taskset -c $OTHER_CPU awk '{print strftime("%FT%T%z"), $0; fflush()}'
}

# Start monitoring power.
powermeter |& ts > $bd/powermeter &
sudo taskset -c $OTHER_CPU stdbuf -oL $BUILD/tools/amdpstate rapl 1000 |& ts > $bd/rapl &

# Run test/micro with all parameter variations.
run_micro() {
	for membench in $MEMORY_BENCH; do
		for cpubench in $CPU_BENCH; do
			for memratio in $MEMORY_RATIO; do
				memratio_opt=
				if [[ $memratio != 1 ]]; then
					memratio_opt=--memory-ratio=$memratio
				fi
				for cpuratio in $CPU_RATIO; do
					cpuratio_opt=
					if [[ $cpuratio != 1 ]]; then
						cpuratio_opt=--cpu-ratio=$cpuratio
					fi

					cmd=($membench $cpubench $memratio_opt $cpuratio_opt)
					log $(date -Ins) start: "$@" $cmd
					time env "$@" $cmd &>> $bd/log
					log $(date -Ins) end: "$@" $cmd

					# Power meter measurements are a bit delayed and need
					# smoothing. Sleep a bit to make sure that power usage from
					# one benchmark doesn't bleed into the next.
					log sleeping...
					sleep 5
				done
			done
		done
	done
}

log $(date -Ins) start fast/slow ultmigration
for i in $(seq $ITERATIONS); do
	run_micro $BUILD/test/micro
done
log $(date -Ins) end fast/slow ultmigration

log $(date -Ins) start idle
sleep 60
log $(date -Ins) end idle

# During the baseline runs, run ult_idle to make results comparable.
log Starting ult_idle...
FAST_CPU=$IDLE_FAST_CPU SLOW_CPU=$IDLE_SLOW_CPU $BUILD/test/ult_idle slow &

log $(date -Ins) start only fast baseline
for i in $(seq $ITERATIONS); do
	run_micro LD_PRELOAD=$BUILD/libultmigration_dummy.so taskset -c $FAST_CPU $BUILD/test/micro
done
log $(date -Ins) end only fast baseline

log $(date -Ins) start only slow baseline
for i in $(seq $ITERATIONS); do
	run_micro LD_PRELOAD=$BUILD/libultmigration_dummy.so taskset -c $SLOW_CPU $BUILD/test/micro
done
log $(date -Ins) end only slow baseline

# Set core 2 to P1.
echo "${frequencies[2]}" | sudo tee /sys/devices/system/cpu/cpu{2,3}/cpufreq/scaling_setspeed > /dev/null
for fid in $CPU_FIDS; do
	sudo $BUILD/tools/amdpstate def 1 CpuFid=$fid
	sudo $BUILD/tools/amdpstate -call frequency 1000 >> $bd/log
	log $(date -Ins) start CpuFid $fid
	for i in $(seq $ITERATIONS); do
		run_micro LD_PRELOAD=$BUILD/libultmigration_dummy.so taskset -c $SLOW_CPU $BUILD/test/micro
	done
	log $(date -Ins) end CpuFid $fid
done

log Killing ult_idle...
kill -SIGTERM %%

# Reset frequency settings.
echo "${frequencies[3]}" | sudo tee /sys/devices/system/cpu/cpu{2,3}/cpufreq/scaling_setspeed > /dev/null

if ((RUN_SWP)); then

	log $(date -Ins) start swp on fast
	for i in $(seq $ITERATIONS); do
		run_micro LD_PRELOAD=$BUILD/libultmigration_dummy.so taskset -c $FAST_CPU $BUILD/test/micro_swp
	done
	log $(date -Ins) end swp on fast

	log $(date -Ins) start swp on slow
	for i in $(seq $ITERATIONS); do
		run_micro LD_PRELOAD=$BUILD/libultmigration_dummy.so taskset -c $SLOW_CPU $BUILD/test/micro_swp
	done
	log $(date -Ins) end swp on slow

fi

log $(date -Ins) Benchmark end
